#!/bin/csh
# Version: $Revision$
# By $Author$
# Change the icc into engineering mode - kill all robotic control processes
# Usage:
#	raptor_engineering_mode
# To return to robotic control, reboot the icc.
echo "Switching to engineering mode."
echo "This will only succeed if you are root."
set hostname = `/usr/bin/hostname`
#
# Kill any running processes
#
if ( -x /icc/bin/scripts/killhup.csh ) then
    echo "Trying to kill AutoBooter."
    /icc/bin/scripts/killhup.csh AutoBooter
else
    echo "killhup not found:FAILED to stop running processess."
endif
# setup LD_LIBRARY_PATH for send_command
if ( ${?LD_LIBRARY_PATH} == 0 ) then
    setenv LD_LIBRARY_PATH ".:/icc/bin/lib/x86_64-linux"
endif
foreach directory ( /icc/bin/lib/x86_64-linux )
    echo "$LD_LIBRARY_PATH" | /bin/grep -q $directory
    if ( $status != 0 ) then
	setenv LD_LIBRARY_PATH ${LD_LIBRARY_PATH}":"${directory}
    endif
end
# Send a shutdown command to moptop C layer, after killing the autobooter
if ( -x /icc/bin/commandserver/test/x86_64-linux/send_command  ) then
    echo "Sending shutdown to raptor C layer."
    /icc/bin/commandserver/test/x86_64-linux/send_command -h raptor -p 1111 -c "shutdown"
    echo "Waiting to see if that worked..."
    /bin/sleep 5
else
    echo "send_command not found:FAILED to stop running moptop."
endif
if ( -x /icc/bin/scripts/killhup.csh ) then
    echo "Trying to kill Raptor."
    /icc/bin/scripts/killhup.csh Raptor
    echo "Trying to kill icsd_inet."
    /icc/bin/scripts/killhup.csh icsd_inet
# kill this last, as it probably kills the raptor_engineering_mode script as well.
    echo "Trying to kill raptor (if shutdown didn't already kill it)."
    /icc/bin/scripts/killhup.csh raptor
else
    echo "killhup not found:ENGINEERING mode FAILED."
endif
